buildscript {
  repositories {
    mavenCentral()
    mavenLocal()
  }

  dependencies {
    // import this for the TransformationTask
    classpath 'com.darylteo.gradle:javassist-plugin:0.2.2'

    // you only need this if you're doing configuration based configuration
    // or if you're creating the ClassTransformer in script rather than in buildSrc
    classpath 'com.github.stephanenicolas.afterburner:afterburner-library:1.0.1-SNAPSHOT'
    classpath 'com.github.stephanenicolas.afterburner:afterburner-sample-processor:1.0.1-SNAPSHOT'
  }
}

apply plugin: 'java'

import com.github.stephanenicolas.afterburner.AfterBurner
import com.github.stephanenicolas.afterburner.InsertableMethodBuilder
import com.github.stephanenicolas.afterburner.inserts.InsertableMethod
import com.darylteo.gradle.javassist.tasks.TransformationTask

// configuration based transformation
task transform1(type: TransformationTask) {
  from sourceSets.main.output
  into("${buildDir}/transformations/transform1")

  def afterBurner = new AfterBurner();

  where { c -> c.name.endsWith('Example')
  }.transform { c ->
    logger.debug("Transforming "+c.getName());

    afterBurner.afterOverrideMethod(c, "doStuff", "System.out.println(\"Inside doStuff\");");

    def builder = new InsertableMethodBuilder(afterBurner);
    builder
        .insertIntoClass(c)
        .inMethodIfExists("doOtherStuff")
        .beforeACallTo("bar")
        .withBody("System.out.println(\"Inside doOtherStuff\");")
        .elseCreateMethodIfNotExists("public void doOtherStuff() { " + InsertableMethod.BODY_TAG + " }")
        .doIt();
  }
}

// transformation with transformer implementation
task transform2(type: TransformationTask) {
  from sourceSets.main.output
  into("${buildDir}/transformations/transform2")

  transformation = new com.github.stephanenicolas.afterburner.sample.ExampleProcessor()
}

task copyTransformed1(type: Copy) {
  from ("${buildDir}/transformations/transform1")
  into  ("${buildDir}/classes/main")
  outputs.upToDateWhen {
    false
  }
}

task copyTransformed2(type: Copy) {
  from ("${buildDir}/transformations/transform2")
  into  ("${buildDir}/classes/main")
}

copyTransformed1.mustRunAfter transform1
copyTransformed2.mustRunAfter transform2

tasks.withType(Copy) {
  eachFile { println "Copied:" + it.path }
  println sourceSets.main.output
}

tasks.withType(TransformationTask) {
  println source
}

task execute(type:JavaExec) {
  main = "com.github.stephanenicolas.afterburner.sample.Main"
  classpath = sourceSets.main.runtimeClasspath
  inputs.file ("${buildDir}/classes/main")
}

task check1() {
  outputs.upToDateWhen {
    false
  }
}

task check2() {
  outputs.upToDateWhen {
    false
  }
}

check1.dependsOn clean, transform1, copyTransformed1, execute
check2.dependsOn clean, transform2, copyTransformed2, execute
